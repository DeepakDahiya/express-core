# Copyright (c) 2020 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at http://mozilla.org/MPL/2.0/.

import("//express/browser/autocomplete/sources.gni")
import("//express/browser/brave_adaptive_captcha/sources.gni")
import("//express/browser/brave_ads/sources.gni")
import("//express/browser/brave_news/sources.gni")
import("//express/browser/brave_referrals/sources.gni")
import("//express/browser/brave_rewards/sources.gni")
import("//express/browser/brave_shields/sources.gni")
import("//express/browser/brave_stats/sources.gni")
import("//express/browser/brave_vpn/sources.gni")
import("//express/browser/brave_wallet/android/sources.gni")
import("//express/browser/brave_wallet/notifications/sources.gni")
import("//express/browser/browsing_data/sources.gni")
import("//express/browser/debounce/sources.gni")
import("//express/browser/download/sources.gni")
import("//express/browser/ephemeral_storage/sources.gni")
import("//express/browser/ethereum_remote_client/buildflags/buildflags.gni")
import("//express/browser/greaselion/sources.gni")
import("//express/browser/importer/sources.gni")
import("//express/browser/infobars/sources.gni")
import("//express/browser/ipfs/sources.gni")
import("//express/browser/misc_metrics/sources.gni")
import("//express/browser/new_tab/sources.gni")
import("//express/browser/notifications/sources.gni")
import("//express/browser/ntp_background/sources.gni")
import("//express/browser/perf/sources.gni")
import("//express/browser/permissions/sources.gni")
import("//express/browser/playlist/sources.gni")
import("//express/browser/request_otr/sources.gni")
import("//express/browser/search_engines/sources.gni")
import("//express/browser/skus/sources.gni")
import("//express/browser/speedreader/sources.gni")
import("//express/browser/sync/sources.gni")
import("//express/browser/themes/sources.gni")
import("//express/browser/url_sanitizer/sources.gni")
import("//express/browser/web_discovery/sources.gni")
import("//express/build/config.gni")
import("//express/chromium_src/chrome/browser/prefs/sources.gni")
import("//express/chromium_src/chrome/browser/sources.gni")
import("//express/components/ai_chat/common/buildflags/buildflags.gni")
import("//express/components/brave_vpn/common/buildflags/buildflags.gni")
import("//express/components/brave_wayback_machine/buildflags/buildflags.gni")
import("//express/components/brave_webtorrent/browser/buildflags/buildflags.gni")
import("//express/components/commander/common/buildflags/buildflags.gni")
import("//express/components/tor/buildflags/buildflags.gni")
import("//extensions/buildflags/buildflags.gni")

brave_chrome_browser_visibility = [
  "//express/browser/*",
  "//express/test:*",

  # these are layering violation and need to be fixed
  "//express/components/ipfs/test:*",

  # Chrome does not yet define a visibility list by itself, meaning we can't
  # append our list to them and that what we have here will become law not just
  # for Brave, but also for the rest of chrome. Thus, we need to provide our own
  # permissive list for chrome here too, to be removed in the future if Chrome
  # ever provides it's own list (at which point we'll append instead of set).
  "//chrome:dependencies",
  "//chrome/app/*",
  "//chrome/test/*",
  "//extensions/*",
]

brave_chrome_browser_sources = [
  "//express/browser/brave_browser_features.cc",
  "//express/browser/brave_browser_features.h",
  "//express/browser/brave_browser_main_extra_parts.cc",
  "//express/browser/brave_browser_main_extra_parts.h",
  "//express/browser/brave_browser_main_parts.cc",
  "//express/browser/brave_browser_main_parts.h",
  "//express/browser/brave_browser_process.cc",
  "//express/browser/brave_browser_process_impl.cc",
  "//express/browser/brave_browser_process_impl.h",
  "//express/browser/brave_content_browser_client.cc",
  "//express/browser/brave_content_browser_client.h",
  "//express/browser/brave_features_internal_names.h",
  "//express/browser/brave_local_state_prefs.cc",
  "//express/browser/brave_local_state_prefs.h",
  "//express/browser/brave_profile_prefs.cc",
  "//express/browser/brave_profile_prefs.h",
  "//express/browser/brave_shell_integration.cc",
  "//express/browser/brave_shell_integration.h",
  "//express/browser/brave_tab_helpers.cc",
  "//express/browser/brave_tab_helpers.h",
  "//express/browser/browser_context_keyed_service_factories.cc",
  "//express/browser/browser_context_keyed_service_factories.h",
  "//express/browser/geolocation/brave_geolocation_permission_context_delegate.cc",
  "//express/browser/geolocation/brave_geolocation_permission_context_delegate.h",
  "//express/browser/metrics/brave_metrics_service_accessor.cc",
  "//express/browser/metrics/brave_metrics_service_accessor.h",
  "//express/browser/metrics/metrics_reporting_util.cc",
  "//express/browser/metrics/metrics_reporting_util.h",
  "//express/browser/policy/brave_simple_policy_map.h",
  "//express/browser/prefs/brave_pref_service_incognito_allowlist.cc",
  "//express/browser/prefs/brave_pref_service_incognito_allowlist.h",
  "//express/browser/translate/brave_translate_prefs_migration.cc",
  "//express/browser/translate/brave_translate_prefs_migration.h",
  "//express/browser/translate/brave_translate_utils.cc",
  "//express/browser/translate/brave_translate_utils.h",
  "//express/browser/update_util.cc",
  "//express/browser/update_util.h",
  "//express/chromium_src/chrome/browser/notifications/notification_handler_impl.h",
]

brave_chrome_browser_deps = [
  "//base",
  "//express/app:brave_generated_resources_grit",
  "//express/browser:browser_process",
  "//express/browser:sparkle_buildflags",
  "//express/browser/brave_federated",
  "//express/browser/brave_wallet",
  "//express/browser/brave_wallet:brave_wallet_delegate",
  "//express/browser/brave_wallet:tab_helper",
  "//express/browser/component_updater",
  "//express/browser/ethereum_remote_client/buildflags",
  "//express/browser/gcm_driver",
  "//express/browser/metrics/buildflags",
  "//express/browser/net",
  "//express/browser/profiles",
  "//express/browser/renderer_context_menu",
  "//express/browser/search",
  "//express/browser/sync",
  "//express/browser/themes",
  "//express/browser/ui",
  "//express/browser/ui/tabs",
  "//express/common",
  "//express/components/ai_chat/common/buildflags",
  "//express/components/brave_ads/browser",
  "//express/components/brave_ads/core",
  "//express/components/brave_component_updater/browser",
  "//express/components/brave_federated",
  "//express/components/brave_new_tab_ui:mojom",
  "//express/components/brave_news/common:mojom",
  "//express/components/brave_perf_predictor/browser",
  "//express/components/brave_private_new_tab_ui/common",
  "//express/components/brave_private_new_tab_ui/common:mojom",
  "//express/components/brave_referrals/browser",
  "//express/components/brave_rewards/common",
  "//express/components/brave_rewards/common:features",
  "//express/components/brave_rewards/common/buildflags",
  "//express/components/brave_rewards/common/mojom",
  "//express/components/brave_search/browser",
  "//express/components/brave_search/common",
  "//express/components/brave_search/common:mojom",
  "//express/components/brave_shields/common",
  "//express/components/brave_shields/common:mojom",
  "//express/components/brave_sync",
  "//express/components/brave_sync:features",
  "//express/components/brave_sync:network_time_helper",
  "//express/components/brave_sync:prefs",
  "//express/components/brave_vpn/common/buildflags",
  "//express/components/brave_vpn/common/mojom",
  "//express/components/brave_wallet/browser",
  "//express/components/brave_wallet/browser:utils",
  "//express/components/brave_wallet/common",
  "//express/components/brave_wallet/common:mojom",
  "//express/components/brave_wallet/common:pref_names",
  "//express/components/brave_wayback_machine/buildflags",
  "//express/components/brave_webtorrent/browser/buildflags",
  "//express/components/commander/common/buildflags",
  "//express/components/constants",
  "//express/components/cosmetic_filters/browser",
  "//express/components/cosmetic_filters/common:mojom",
  "//express/components/de_amp/browser",
  "//express/components/de_amp/common",
  "//express/components/decentralized_dns/content",
  "//express/components/decentralized_dns/core",
  "//express/components/google_sign_in_permission",
  "//express/components/greaselion/browser/buildflags",
  "//express/components/https_upgrade_exceptions/browser",
  "//express/components/ipfs/buildflags",
  "//express/components/l10n/common",
  "//express/components/localhost_permission",
  "//express/components/ntp_background_images/browser",
  "//express/components/ntp_background_images/buildflags",
  "//express/components/ntp_tiles",
  "//express/components/p3a",
  "//express/components/p3a:buildflags",
  "//express/components/playlist/common/buildflags",
  "//express/components/privacy_sandbox",
  "//express/components/request_otr/common/buildflags",
  "//express/components/resources",
  "//express/components/skus/browser",
  "//express/components/skus/common",
  "//express/components/speedreader/common/buildflags",
  "//express/components/tor/buildflags",
  "//express/components/version_info",
  "//express/services/network/public/cpp",
  "//express/third_party/blink/renderer",
  "//chrome/browser:browser_process",
  "//chrome/browser/profiles:profile",
  "//chrome/browser/ui",
  "//chrome/common",
  "//chrome/common:channel_info",
  "//chrome/utility",
  "//components/account_id",
  "//components/autofill/core/common",
  "//components/browsing_data/core",
  "//components/content_settings/browser",
  "//components/content_settings/core/browser",
  "//components/content_settings/core/common",
  "//components/embedder_support",
  "//components/feed/core/shared_prefs:feed_shared_prefs",
  "//components/gcm_driver:gcm_buildflags",
  "//components/metrics",
  "//components/ntp_tiles",
  "//components/password_manager/core/common",
  "//components/permissions",
  "//components/policy/core/common:common_constants",
  "//components/pref_registry",
  "//components/prefs",
  "//components/privacy_sandbox:privacy_sandbox_prefs",
  "//components/safe_browsing/core/common:safe_browsing_prefs",
  "//components/search_engines",
  "//components/services/heap_profiling/public/mojom",
  "//components/signin/public/base",
  "//components/sync/base",
  "//components/sync/service",
  "//components/translate/core/browser:translate_pref_names",
  "//components/version_info",
  "//components/version_info:channel",
  "//content/public/browser",
  "//content/public/common",
  "//extensions/buildflags",
  "//mojo/public/cpp/bindings",
  "//net",
  "//services/metrics/public/cpp:metrics_cpp",
  "//services/network/public/cpp",
  "//third_party/abseil-cpp:absl",
  "//third_party/blink/public/mojom:mojom_platform_headers",
  "//third_party/widevine/cdm:buildflags",
  "//ui/base",
  "//url",
]

if (enable_commander) {
  brave_chrome_browser_deps += [
    "//express/components/commander/browser",
    "//express/components/commands/browser",
    "//express/components/commands/common",
  ]
}

if (is_mac) {
  brave_chrome_browser_sources += [
    "//express/browser/brave_app_controller_mac.h",
    "//express/browser/brave_app_controller_mac.mm",
    "//express/browser/brave_browser_main_parts_mac.h",
    "//express/browser/brave_browser_main_parts_mac.mm",
    "//express/browser/brave_shell_integration_mac.h",
    "//express/browser/brave_shell_integration_mac.mm",
  ]
  brave_chrome_browser_deps += [ "//express/app:command_ids" ]
}

if (enable_sparkle) {
  brave_chrome_browser_sources += [
    "//express/browser/mac/sparkle_glue.h",
    "//express/browser/mac/sparkle_glue.mm",
    "//express/browser/mac/su_updater.h",
  ]
}

if (enable_speedreader) {
  brave_chrome_browser_deps += [
    "//express/components/speedreader/common",
    "//express/components/speedreader/common:mojom",
  ]
}

if (enable_request_otr) {
  brave_chrome_browser_deps += [ "//express/components/request_otr/common" ]
}

if (enable_ai_chat) {
  brave_chrome_browser_deps += [
    "//express/components/ai_chat/browser",
    "//express/components/ai_chat/common",
    "//express/components/ai_chat/common/mojom",
    "//express/components/ai_chat/core",
  ]
}

if (enable_brave_vpn) {
  brave_chrome_browser_deps += [
    "//express/components/brave_vpn/browser",
    "//express/components/brave_vpn/browser/connection",
    "//express/components/brave_vpn/browser/connection:api",
    "//express/components/brave_vpn/common",
  ]
}

if (ethereum_remote_client_enabled) {
  brave_chrome_browser_deps += [
    "//express/browser/ethereum_remote_client",
    "//express/browser/ethereum_remote_client:browser",
  ]
}

if (is_android) {
  brave_chrome_browser_sources += [
    "//express/browser/android/brave_init_android.cc",
    "//express/browser/sync/brave_sync_devices_android.cc",
    "//express/browser/sync/brave_sync_devices_android.h",
    "//chrome/browser/bookmarks/bookmark_html_writer.cc",
    "//chrome/browser/bookmarks/bookmark_html_writer.h",
    "//chrome/browser/importer/profile_writer.cc",
    "//chrome/browser/importer/profile_writer.h",
  ]

  brave_chrome_browser_sources += brave_browser_brave_wallet_android_sources
  brave_chrome_browser_sources += brave_browser_misc_metrics_android_sources
  brave_chrome_browser_deps += brave_browser_brave_wallet_android_deps

  brave_chrome_browser_deps += [
    "//express/browser/android:android_browser_process",
    "//express/browser/android/preferences",
    "//express/browser/android/safe_browsing:buildflags",
    "//express/build/android:jni_headers",
    "//express/components/brave_sync:sync_service_impl_helper",
    "//chrome/android:jni_headers",
    "//components/sync_device_info",
  ]
} else {
  brave_chrome_browser_sources += [
    "//express/browser/p3a/p3a_core_metrics.cc",
    "//express/browser/p3a/p3a_core_metrics.h",
    "//express/browser/profile_resetter/brave_profile_resetter.cc",
    "//express/browser/profile_resetter/brave_profile_resetter.h",
  ]

  brave_chrome_browser_deps += [
    "//express/app:brave_generated_resources_grit",
    "//express/browser/brave_ads/tooltips",
    "//express/browser/importer",
    "//express/browser/ui/bookmark",
    "//express/components/brave_search_conversion",
    "//express/components/time_period_storage",
    "//components/infobars/content",
  ]
}

if (toolkit_views) {
  brave_chrome_browser_deps += [
    "//express/browser/ui/sidebar",
    "//express/components/sidebar",
  ]
}

if (enable_tor) {
  brave_chrome_browser_deps += [
    "//express/browser/tor",
    "//express/components/tor",
    "//express/components/tor:common",
    "//express/components/tor:pref_names",
  ]
}

if (enable_extensions) {
  brave_chrome_browser_deps += [
    "//express/browser/extensions",
    "//chrome/browser/extensions",
    "//extensions/browser",
    "//extensions/common:common_constants",
  ]

  if (enable_brave_webtorrent) {
    brave_chrome_browser_deps +=
        [ "//express/components/brave_webtorrent/browser" ]
  }
}

if (enable_brave_wayback_machine) {
  brave_chrome_browser_deps += [ "//express/components/brave_wayback_machine" ]
}

if (enable_widevine) {
  brave_chrome_browser_sources += [
    "//express/browser/brave_drm_tab_helper.cc",
    "//express/browser/brave_drm_tab_helper.h",
  ]
  brave_chrome_browser_deps += [
    "//express/browser/widevine",
    "//express/components/brave_drm",
    "//express/components/widevine:constants",
  ]
}

if (is_win) {
  brave_chrome_browser_sources += [
    "//express/browser/brave_shell_integration_win.cc",
    "//express/browser/brave_shell_integration_win.h",
    "//express/browser/default_protocol_handler_utils_win.cc",
    "//express/browser/default_protocol_handler_utils_win.h",
  ]

  brave_chrome_browser_deps += [
    "//chrome/install_static:install_static_util",
    "//chrome/installer/util:with_no_strings",
  ]
}

if (is_win && is_official_build) {
  brave_chrome_browser_sources += [
    "//chrome/browser/google/google_update_win.cc",
    "//chrome/browser/google/google_update_win.h",
    "//chrome/browser/google/switches.cc",
    "//chrome/browser/google/switches.h",
  ]

  brave_chrome_browser_deps += [ "//google_update" ]
}

brave_chrome_browser_public_deps = [
  "//express/browser:browser_process",
  "//express/components/brave_sync:constants",
  "//express/components/variations:constants",
]

if (is_mac) {
  brave_chrome_framework_public_deps = [ "//express:framework_bundle_data" ]
  brave_chrome_framework_bundle_deps = [ "//express:framework_bundle_data" ]
  brave_chrome_framework_contents = [ "Frameworks" ]
  brave_chrome_framework_configs = [ "//express/build/rust:strip_rust_symbols" ]
}

brave_chrome_browser_sources += brave_browser_autocomplete_sources
brave_chrome_browser_sources += brave_browser_brave_adaptive_captcha_sources
brave_chrome_browser_sources += brave_browser_brave_ads_sources
brave_chrome_browser_sources += brave_browser_brave_wallet_sources
brave_chrome_browser_sources += brave_browser_brave_news_sources
brave_chrome_browser_sources += brave_browser_brave_vpn_sources
brave_chrome_browser_sources += brave_browser_skus_sources
brave_chrome_browser_sources += brave_browser_brave_rewards_sources
brave_chrome_browser_sources += brave_browser_brave_shields_sources
brave_chrome_browser_sources += brave_browser_brave_stats_updater_sources
brave_chrome_browser_sources += brave_browser_browsing_data_sources
brave_chrome_browser_sources += brave_browser_debounce_sources
brave_chrome_browser_sources += brave_browser_download_sources
brave_chrome_browser_sources += brave_browser_ephemeral_storage_sources
brave_chrome_browser_sources += brave_browser_greaselion_sources
brave_chrome_browser_sources += brave_browser_importer_sources
brave_chrome_browser_sources += brave_browser_infobar_sources
brave_chrome_browser_sources += brave_browser_ipfs_sources
brave_chrome_browser_sources += brave_browser_misc_metrics_sources
brave_chrome_browser_sources += brave_browser_new_tab_sources
brave_chrome_browser_sources += brave_browser_notifications_sources
brave_chrome_browser_sources += brave_browser_ntp_background_sources
brave_chrome_browser_sources += brave_browser_perf_sources
brave_chrome_browser_sources += brave_browser_permissions_sources
brave_chrome_browser_sources += brave_browser_playlist_sources
brave_chrome_browser_sources += brave_browser_request_otr_sources
brave_chrome_browser_sources += brave_browser_search_engines_sources
brave_chrome_browser_sources += brave_browser_speedreader_sources
brave_chrome_browser_sources += brave_browser_sync_sources
brave_chrome_browser_sources += brave_browser_themes_sources
brave_chrome_browser_sources += brave_browser_web_discovery_sources
brave_chrome_browser_sources += brave_browser_url_sanitizer_sources
brave_chrome_browser_sources += brave_browser_brave_referrals_sources

brave_chrome_browser_deps += brave_browser_autocomplete_deps
brave_chrome_browser_deps += brave_browser_brave_adaptive_captcha_deps
brave_chrome_browser_deps += brave_browser_brave_ads_deps
brave_chrome_browser_deps += brave_browser_brave_news_deps
brave_chrome_browser_deps += brave_browser_brave_vpn_deps
brave_chrome_browser_deps += brave_browser_brave_wallet_deps
brave_chrome_browser_deps += brave_browser_skus_deps
brave_chrome_browser_deps += brave_browser_brave_rewards_deps
brave_chrome_browser_deps += brave_browser_brave_shields_deps
brave_chrome_browser_deps += brave_browser_brave_stats_updater_deps
brave_chrome_browser_deps += brave_browser_browsing_data_deps
brave_chrome_browser_deps += brave_browser_debounce_deps
brave_chrome_browser_deps += brave_browser_download_deps
brave_chrome_browser_deps += brave_browser_ephemeral_storage_deps
brave_chrome_browser_deps += brave_browser_greaselion_deps
brave_chrome_browser_deps += brave_browser_importer_deps
brave_chrome_browser_deps += brave_browser_infobar_deps
brave_chrome_browser_deps += brave_browser_ipfs_deps
brave_chrome_browser_deps += brave_browser_misc_metrics_deps
brave_chrome_browser_deps += brave_browser_new_tab_deps
brave_chrome_browser_deps += brave_browser_notifications_deps
brave_chrome_browser_deps += brave_browser_ntp_background_deps
brave_chrome_browser_deps += brave_browser_perf_deps
brave_chrome_browser_deps += brave_browser_permissions_deps
brave_chrome_browser_deps += brave_browser_playlist_deps
brave_chrome_browser_deps += brave_browser_request_otr_deps
brave_chrome_browser_deps += brave_browser_search_engines_deps
brave_chrome_browser_deps += brave_browser_speedreader_deps
brave_chrome_browser_deps += brave_browser_sync_deps
brave_chrome_browser_deps += brave_browser_themes_deps
brave_chrome_browser_deps += brave_browser_web_discovery_deps
brave_chrome_browser_deps += brave_chromium_src_chrome_browser_deps
brave_chrome_browser_deps += brave_chromium_src_chrome_browser_prefs_deps
brave_chrome_browser_deps += brave_browser_url_sanitizer_deps
brave_chrome_browser_deps += brave_browser_brave_referrals_deps

brave_chrome_browser_allow_circular_includes_from = [ "//express/browser/ui" ]
