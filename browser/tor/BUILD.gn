# Copyright (c) 2022 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//express/components/tor/buildflags/buildflags.gni")
import("//extensions/buildflags/buildflags.gni")

assert(enable_tor)

source_set("tor") {
  # Remove when https://github.com/brave/brave-browser/issues/10655 is resolved
  check_includes = false
  visibility = [
    ":*",
    "//express/browser",
    "//express/browser/decentralized_dns/test:unit_tests",
    "//express/browser/ipfs/test:*",
    "//express/browser/net:browser_tests",
    "//express/browser/profiles",
    "//express/browser/ui",
    "//express/test:*",
    "//chrome/browser",
  ]

  sources = [
    "onion_location_navigation_throttle_delegate.cc",
    "onion_location_navigation_throttle_delegate.h",
    "tor_profile_manager.cc",
    "tor_profile_manager.h",
    "tor_profile_service_factory.cc",
    "tor_profile_service_factory.h",
  ]

  deps = [
    "//base",
    "//express/components/brave_webtorrent/browser/buildflags",
    "//express/components/constants",
    "//express/components/tor",
    "//express/components/tor:pref_names",
    "//chrome/common",
    "//components/autofill/content/browser",
    "//components/autofill/core/browser",
    "//components/keyed_service/content",
    "//components/safe_browsing/core/common:safe_browsing_prefs",
    "//components/translate/core/browser",
    "//content/public/browser",
    "//net",
    "//third_party/blink/public/common",
    "//url",
  ]

  # Below dep list are not directly used tor target.
  # Only added to fix intermittent build failure.
  # Most of the case comes from including brave_browser_process_impl.h`
  # headers. That header includes some buildflags but sometimes they are
  # not generated before evaluating tor target.
  deps += [
    "//express/components/greaselion/browser/buildflags",
    "//express/components/speedreader/common/buildflags",
  ]
}

source_set("unit_tests") {
  testonly = true

  sources = [
    "tor_navigation_throttle_unittest.cc",
    "tor_profile_manager_unittest.cc",
  ]

  deps = [
    "//base",
    "//base/test:test_support",
    "//express/browser/tor",
    "//express/components/brave_webtorrent/browser/buildflags",
    "//express/components/constants",
    "//express/components/tor",
    "//express/components/tor:test_support",
    "//chrome/browser/devtools",
    "//chrome/common",
    "//chrome/test:test_support",
    "//components/keyed_service/content",
    "//components/safe_browsing/core/common:safe_browsing_prefs",
    "//components/translate/core/browser",
    "//content/public/browser",
    "//content/test:test_support",
    "//testing/gtest",
    "//third_party/blink/public/common",
    "//url",
  ]
}

source_set("browser_tests") {
  testonly = true
  sources = [
    "//chrome//browser/net/profile_network_context_service_test_utils.cc",
    "//chrome//browser/net/profile_network_context_service_test_utils.h",
    "brave_local_state_browsertest.cc",
    "brave_tor_browsertest.cc",
    "onion_domain_throttle_browsertest.cc",
    "onion_location_navigation_throttle_browsertest.cc",
    "tor_profile_manager_browsertest.cc",
  ]

  deps = [
    "//base",
    "//express/app:brave_generated_resources_grit",
    "//express/browser",
    "//express/browser/tor",
    "//express/common",
    "//express/components/brave_ads/browser",
    "//express/components/brave_component_updater/browser",
    "//express/components/brave_shields/browser",
    "//express/components/constants",
    "//express/components/ipfs/buildflags",
    "//express/components/l10n/common",
    "//express/components/tor",
    "//express/components/tor:pref_names",
    "//express/components/tor:test_support",
    "//chrome/browser",
    "//chrome/browser:browser_process",
    "//chrome/browser/ui",
    "//chrome/test:test_support",
    "//chrome/test:test_support_ui",
    "//components/bookmarks/browser",
    "//components/bookmarks/common",
    "//content/public/browser",
    "//content/test:test_support",
    "//net:test_support",
    "//ui/views:test_support",
  ]

  if (enable_extensions) {
    deps += [
      "//extensions/browser",
      "//extensions/common",
    ]
  }

  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
}
