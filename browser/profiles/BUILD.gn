# Copyright (c) 2018 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//express/components/tor/buildflags/buildflags.gni")
import("//components/gcm_driver/config.gni")
import("//extensions/buildflags/buildflags.gni")

source_set("profiles") {
  # Remove when https://github.com/brave/brave-browser/issues/10648 is resolved
  check_includes = false
  sources = [
    "brave_bookmark_model_loaded_observer.cc",
    "brave_bookmark_model_loaded_observer.h",
    "brave_profile_avatar_downloader.cc",
    "brave_profile_avatar_downloader.h",
    "brave_profile_manager.cc",
    "brave_profile_manager.h",
    "brave_renderer_updater.cc",
    "brave_renderer_updater.h",
    "brave_renderer_updater_factory.cc",
    "brave_renderer_updater_factory.h",
  ]

  public_deps = [ ":util" ]

  deps = [
    "//base",
    "//express/browser/gcm_driver",
    "//express/common",
    "//express/components/brave_ads/browser",
    "//express/components/brave_rewards/browser",
    "//express/components/brave_sync:prefs",
    "//express/components/brave_wallet/browser",
    "//express/components/brave_wallet/common",
    "//express/components/constants",
    "//express/components/ipfs/buildflags",
    "//express/components/tor/buildflags",
    "//chrome/common",
    "//components/gcm_driver:gcm_buildflags",
    "//components/keyed_service/content",
    "//components/keyed_service/core",
    "//components/prefs",
    "//components/search_engines",
    "//components/signin/public/identity_manager",
    "//mojo/public/cpp/bindings",
    "//ui/base",
  ]

  if (enable_extensions) {
    deps += [ "//extensions/common" ]
  }

  if (use_gcm_from_platform) {
    deps += [ "//express/browser/gcm_driver" ]
  }

  if (enable_tor) {
    deps += [ "//express/browser/tor" ]
  }
}

source_set("util") {
  # Remove when https://github.com/brave/brave-browser/issues/10648 is resolved
  check_includes = false

  sources = [
    "profile_util.cc",
    "profile_util.h",
  ]

  deps = [
    "//base",
    "//express/common",
    "//express/components/ntp_background_images/common",
    "//express/components/search_engines",
    "//express/components/tor/buildflags",
    "//components/prefs",
    "//content/public/browser",
  ]
}
