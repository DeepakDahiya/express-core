# Copyright (c) 2022 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//express/build/config.gni")
import("//build/buildflag_header.gni")
import("//build/config/features.gni")
import("//chrome/common/features.gni")

buildflag_header("sparkle_buildflags") {
  header = "sparkle_buildflags.h"
  flags = [ "ENABLE_SPARKLE=$enable_sparkle" ]
}

if (enable_sparkle && !build_sparkle) {
  bundle_data("prebuilt_sparkle_framework_bundle_data") {
    sources = [ "//build/mac_files/sparkle_binaries/Sparkle.framework" ]
    outputs = [ "{{bundle_contents_dir}}/Frameworks/{{source_file_part}}" ]
  }
}

source_set("browser_process") {
  visibility = [
    "//express/browser/*",
    "//chrome/browser/*",
  ]

  sources = [ "brave_browser_process.h" ]

  deps = [
    "//express/browser/ethereum_remote_client/buildflags",
    "//express/components/brave_vpn/common/buildflags",
    "//express/components/greaselion/browser/buildflags",
    "//express/components/ipfs/buildflags",
    "//express/components/request_otr/common/buildflags",
    "//express/components/speedreader/common/buildflags",
    "//express/components/tor/buildflags",
    "//chrome/browser:browser_process",
    "//extensions/buildflags",
  ]
}

group("browser") {
  visibility = [
    "//express:browser_dependencies",
    "//express/browser/*",
    "//express/components/brave_rewards/core:headers",
    "//express/test:*",

    # these are layering violation and need to be fixed
    "//express/components/brave_rewards/browser:testutil",
    "//express/components/brave_rewards/test:brave_rewards_unit_tests",
    "//express/components/ipfs/test:*",
  ]

  if (enable_sparkle) {
    if (build_sparkle) {
      deps = [ "//express/vendor/sparkle:sparkle_framework_bundle_data" ]
    } else {
      deps = [ ":prebuilt_sparkle_framework_bundle_data" ]
    }
  }

  # Group depending on upstream's counterpart to make it easier to manage
  # dependencies in brave/ (//chrome/browser does not depend on this target
  # so //express/browser becomes essentially an alias for //chrome/browser now).
  public_deps = [ "//chrome/browser" ]
}

source_set("unit_tests") {
  testonly = true

  sources = []
  deps = []

  if (is_win) {
    sources += [ "default_protocol_handler_utils_win_unittest.cc" ]

    deps += [
      "//base",
      "//express/browser",
      "//testing/gtest",
    ]
  }
}
